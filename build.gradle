buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
        classpath 'org.owasp:dependency-check-gradle:5.3.2'
        classpath 'org.openjfx:javafx-plugin:0.0.9'
        classpath 'org.beryx.runtime:org.beryx.runtime.gradle.plugin:1.11.1'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'org.beryx.runtime'

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencyCheck {
    analyzers {
        assemblyEnabled = false
    }

    failBuildOnCVSS = 5
    failOnError = true
}

sourceCompatibility = 11
targetCompatibility = 11

group 'uk.co.mpcontracting'
version = '5.0.0'

jar {
    archivesBaseName = "${project.name}"
}

javafx {
    version = "14.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.media' ]
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'javax.validation:validation-api'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.apache.lucene:lucene-core:6.1.0'
    implementation 'org.apache.lucene:lucene-queries:6.1.0'
    implementation 'org.apache.lucene:lucene-analyzers-common:6.1.0'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.google.code.gson:gson'
    implementation 'com.igormaznitsa:commons-version:1.0.0'
    implementation 'org.eclipse.jetty:jetty-servlet'
    implementation 'net.java.dev.jna:jna:5.5.0'

    compileOnly 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testfx:testfx-core:4.0.16-alpha'
    testImplementation 'org.testfx:testfx-junit:4.0.15-alpha'
    testImplementation 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'com.google.guava:guava:27.1-jre'

    testCompileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testAnnotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            excludes = [
                    // Exclude RpmJukebox as we cannot test the static initialisation fully
                    'uk.co.mpcontracting.rpmjukebox.RpmJukebox',
                    // Exclude RpmJukeboxLauncher as we cannot test the static initialisation fully
                    'uk.co.mpcontracting.rpmjukebox.RpmJukeboxLauncher',
                    // Exclude MediaManager as we cannot test the MediaPlayer events
                    'uk.co.mpcontracting.rpmjukebox.manager.MediaManager',
                    // Exclude NativeManager as test coverage is only achievable on OSX
                    'uk.co.mpcontracting.rpmjukebox.manager.NativeManager',
                    // Exclude the javafx package as it's imported from a 3rd party project
                    'uk.co.mpcontracting.rpmjukebox.javafx.*'
            ]
        }
    }
}

/////////////////////////
// JavaFX Build Script //
/////////////////////////

application {
    mainClassName = 'uk.co.mpcontracting.rpmjukebox.RpmJukeboxLauncher'
    applicationDefaultJvmArgs = ['-Xmx128m']
}

runtime {
    options.addAll('--no-header-files', '--no-man-pages')
    modules.addAll('java.desktop', 'java.logging', 'java.management', 'java.naming', 'java.scripting', 'java.sql', 'java.xml', 'jdk.jfr', 'jdk.unsupported')

    jpackage {
        appVersion = "${version}"
        imageName = 'RPM Jukebox'
        installerName = "RPM Jukebox"

        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            installerType = 'dmg'
            resourceDir = file("$rootDir/src/main/deploy/package/macosx")
            imageOptions = ['--mac-package-name', 'RPM Jukebox']
        }

        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            resourceDir = file("$rootDir/src/main/deploy/package/windows")
            installerOptions = ['--win-dir-chooser', '--win-menu', '--win-menu-group', 'RPM Jukebox']
        }

        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            resourceDir = file("$rootDir/src/main/deploy/package/linux")
        }
    }
}

/////////////////////////
// JavaFX Build Script //
/////////////////////////

/*jfx {
    appName = 'RPM Jukebox'
    vendor = 'Matt Parker'
    nativeReleaseVersion = "${version}"
    mainClass = 'uk.co.mpcontracting.rpmjukebox.RpmJukebox'
    jfxMainAppJarName = "${project.name}-${version}.jar"
    jvmArgs = ['-Xmx128m']
    //verbose = true
    bundleArguments = [
            'copyright': 'Â©2016-2020 Matt Parker',
            'systemWide': 'true',
            'win.menuGroup': 'RPM Jukebox',
            'win.msi.productVersion': "${version}"
    ]
}*/